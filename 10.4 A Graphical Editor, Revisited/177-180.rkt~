;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-beginner-reader.ss" "lang")((modname 177-180) (read-case-sensitive #t) (teachpacks ((lib "image.rkt" "teachpack" "2htdp"))) (htdp-settings #(#t constructor repeating-decimal #f #t none #f ((lib "image.rkt" "teachpack" "2htdp")) #f)))
(require 2htdp/universe)

(define-struct editor [pre post])
; An Editor is a structure:
;   (make-editor Lo1S Lo1S) 
; An Lo1S is one of: 
; – '()
; – (cons 1String Lo1S)

(define good
  (cons "g" (cons "o" (cons "o" (cons "d" '())))))
(define all
  (cons "a" (cons "l" (cons "l" '()))))
(define lla
  (cons "l" (cons "l" (cons "a" '()))))

; Lo1s -> Lo1s 
; produces a reverse version of the given list 
(check-expect
  (rev (cons "a" (cons "b" (cons "c" '()))))
  (cons "c" (cons "b" (cons "a" '()))))
 
(define (rev l)
  (cond
    [(empty? l) '()]
    [else (add-at-end (rev (rest l)) (first l))]))


; Lo1s 1String -> Lo1s
; creates a new list by adding s to the end of l
(check-expect
 (add-at-end '() "a")
 (cons "a" '()))
(check-expect
  (add-at-end (cons "c" (cons "b" '())) "a")
  (cons "c" (cons "b" (cons "a" '()))))

(define (add-at-end l s)
  (cond
    [(empty? l) (cons s '())]
    [else
     (cons (first l) (add-at-end (rest l) s))]))

; Exercise 174

; String, String -> Editor
; consumes strings s1 s2 and produces an Editor
(check-expect (create-editor "good" "all") (make-editor (reverse good) all))
(define (create-editor s1 s2)
  (make-editor (reverse (explode s1)) (explode s2)))


(define HEIGHT 20) ; the height of the editor 
(define WIDTH 200) ; its width 
(define FONT-SIZE 16) ; the font size 
(define FONT-COLOR "black") ; the font color 
 
(define MT (empty-scene WIDTH HEIGHT))
(define CURSOR (rectangle 1 HEIGHT "solid" "red"))

; Lo1s -> Image
; renders a list of 1Strings as a text image
(check-expect
  (editor-text
   (cons "p" (cons "o" (cons "s" (cons "t" '())))))
  (text "post" FONT-SIZE FONT-COLOR))
(check-expect
  (editor-text
   (cons "p" '()))
  (text "p" FONT-SIZE FONT-COLOR))

;; Exercise 180
(define (editor-text lo1s)
  (cond
    [(empty? lo1s) (text "" FONT-SIZE FONT-COLOR)]
    [else (beside (text (first lo1s) FONT-SIZE FONT-COLOR)
            (editor-text (rest lo1s)))]))

; Editor -> Image
; renders an editor as an image of the two texts 
; separated by the cursor 
(define (editor-render e)
  (place-image/align
    (beside (editor-text (reverse (editor-pre e)))
            CURSOR
            (editor-text (editor-post e)))
    1 1
    "left" "top"
    MT))



; Editor KeyEvent -> Editor
; deals with a key event, given some editor
(check-expect
 (editor-kh (create-editor "" "") "e")
 (create-editor "e" ""))
(check-expect
  (editor-kh (create-editor "cd" "fgh") "e")
  (create-editor "cde" "fgh"))
(check-expect
  (editor-kh (create-editor "cd" "fgh") "\b")
  (create-editor "c" "fgh"))
(check-expect
  (editor-kh (create-editor "cd" "fgh") "left")
  (create-editor "c" "dfgh"))
(check-expect
  (editor-kh (create-editor "" "fgh") "left")
  (create-editor "" "fgh"))
(check-expect
  (editor-kh (create-editor "cd" "fgh") "right")
  (create-editor "cdf" "gh"))
(check-expect
  (editor-kh (create-editor "" "fgh") "right")
  (create-editor "f" "gh"))

(define (editor-kh ed k)
 (cond
    [(key=? k "left") (editor-lft ed)]
    [(key=? k "right") (editor-rgt ed)]
    [(key=? k "\b") (editor-del ed)]
    [(key=? k "\t") ed]
    [(key=? k "\r") ed]
    [(= (string-length k) 1) (editor-ins ed k)]
    [else ed]))



; Exercise 178
; Because "\t" and "\r" have length of 1


; Editor, 1String -> Editor
; insert the 1String k between pre and post
(check-expect
  (editor-ins (make-editor '() '()) "e")
  (make-editor (cons "e" '()) '()))
 
(check-expect
  (editor-ins
    (make-editor (cons "d" '())
                 (cons "f" (cons "g" '())))
    "e")
  (make-editor (cons "e" (cons "d" '()))
               (cons "f" (cons "g" '()))))

(define (editor-ins ed k)
  (make-editor (cons k (editor-pre ed))
               (editor-post ed)))

; Exercise 179

; Editor -> Editor
; moves the cursor position one 1String left, 
; if possible
(check-expect
 (editor-lft (make-editor '() (cons "a" '())))
 (make-editor '() (cons "a" '())))
(check-expect
 (editor-lft (make-editor (cons "a" '()) '()))
 (make-editor '() (cons "a" '())))
(check-expect
 (editor-lft (make-editor (cons "a" (cons "b" '())) '()))
 (make-editor (cons "b" '()) (cons "a" '())))

(define (editor-lft ed)
  (cond
    [(empty? (editor-pre ed)) ed]
    [else (make-editor (rest (editor-pre ed)) (cons (first (editor-pre ed)) (editor-post ed)))]))
 
; Editor -> Editor
; moves the cursor position one 1String right, 
; if possible
(check-expect
 (editor-rgt (make-editor '() (cons "a" '())))
 (make-editor (cons "a" '()) '()))
(check-expect
 (editor-rgt (make-editor '() (cons "a" (cons "b" '()))))
 (make-editor (cons "a" '()) (cons "b" '())))
(check-expect
 (editor-rgt (make-editor (cons "a" '()) '()))
 (make-editor (cons "a" '()) '() ))

(define (editor-rgt ed)
  (cond
    [(empty? (editor-post ed)) ed]
    [else (make-editor (cons (first (editor-post ed)) (editor-pre ed)) (rest (editor-post ed)))]))
 
; Editor -> Editor
; deletes a 1String to the left of the cursor,
; if possible
(check-expect
 (editor-del (make-editor '() (cons "a" '())))
 (make-editor '() (cons "a" '())))
(check-expect
 (editor-del (make-editor (cons "a" '()) '()))
 (make-editor '() '()))
(check-expect
 (editor-del (make-editor (cons "a" (cons "b" '())) '()))
 (make-editor (cons "b" '()) '()))

(define (editor-del ed)
    (cond
    [(empty? (editor-pre ed)) ed]
    [else (make-editor (rest (editor-pre ed)) (editor-post ed))]))

; main : String -> Editor
; launches the editor given some initial string 
(define (main s)
   (big-bang (create-editor s "")
     [on-key editor-kh]
     [to-draw editor-render]))

(main "")